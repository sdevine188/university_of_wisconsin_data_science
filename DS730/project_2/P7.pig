batters = LOAD 'hdfs:/user/maria_dev/pigtest/Batting.csv' using PigStorage(',');
batters_clean = FILTER batters BY $1 > 0;
player_doubles_triples = FOREACH batters_clean GENERATE $0 AS player_id, $8 AS doubles, $9 AS triples;
player_sum_doubles_triples = FOREACH player_doubles_triples GENERATE player_id, doubles, triples, (doubles + triples) AS sum_doubles_triples;
player_sum_doubles_triples_grouped = GROUP player_sum_doubles_triples BY player_id;
player_career_sum_doubles_triples = FOREACH player_sum_doubles_triples_grouped GENERATE group AS player_id, SUM(player_sum_doubles_triples.sum_doubles_triples) AS career_sum_doubles_triples;
master = LOAD 'hdfs:/user/maria_dev/pigtest/Master.csv' using PigStorage(',');
player_birth_city_state = FOREACH master GENERATE $0 AS player_id, $5 AS birth_state, $6 AS birth_city;
player_career_sum_doubles_triples_w_birth_city_state = JOIN player_career_sum_doubles_triples BY (player_id), player_birth_city_state BY (player_id);
player_career_sum_doubles_triples_w_birth_city_state = FILTER player_career_sum_doubles_triples_w_birth_city_state BY (birth_city is not null) AND NOT(birth_city MATCHES '') AND (birth_state is not null) AND NOT(birth_state MATCHES '');
player_career_sum_doubles_triples_w_birth_city_state = FOREACH player_career_sum_doubles_triples_w_birth_city_state GENERATE birth_city, birth_state, career_sum_doubles_triples, SUBSTRING(birth_city, 0, 1) AS birth_city_first_letter;  
player_career_sum_doubles_triples_w_birth_city_state_filtered = FILTER player_career_sum_doubles_triples_w_birth_city_state BY birth_city_first_letter IN ('A', 'E', 'I', 'O', 'U');
player_career_sum_doubles_triples_w_birth_city_state_filtered = FOREACH player_career_sum_doubles_triples_w_birth_city_state_filtered GENERATE CONCAT(birth_city, ', ', birth_state) AS birth_city_state, career_sum_doubles_triples;
player_career_sum_doubles_triples_w_birth_city_state_filtered_grouped = GROUP player_career_sum_doubles_triples_w_birth_city_state_filtered BY birth_city_state;
birth_city_state_sum_doubles_triples = FOREACH player_career_sum_doubles_triples_w_birth_city_state_filtered_grouped GENERATE group AS birth_city_state, SUM(player_career_sum_doubles_triples_w_birth_city_state_filtered.career_sum_doubles_triples) AS sum_doubles_triples;
birth_city_state_sum_doubles_triples_ranked = RANK birth_city_state_sum_doubles_triples BY sum_doubles_triples DESC DENSE;
birth_city_state_sum_doubles_triples_ranked = ORDER birth_city_state_sum_doubles_triples_ranked BY sum_doubles_triples DESC;
birth_city_state_sum_doubles_triples_ranked = FOREACH birth_city_state_sum_doubles_triples_ranked GENERATE $0 AS rank, birth_city_state, sum_doubles_triples;
top_5_birth_city_state = FILTER birth_city_state_sum_doubles_triples_ranked BY rank IN (1, 2, 3, 4, 5);
top_5_birth_city_state = FOREACH top_5_birth_city_state GENERATE birth_city_state;
top_5_birth_city_state = LIMIT top_5_birth_city_state 10;
DUMP top_5_birth_city_state;
